#
# dyndns - update ip-address at dy.fi
#
# This script is scheduled to run once per hour
#

# variables
:local ifname "ether1-gateway"
:local ifip 0.0.0.0
:local ddnsuser "<username>"
:local ddnspass "<password>"
:local ddnsname "<hostname>"
:local ddnsip 0.0.0.0
:local runcount [/system script get dyndns run-count];
:local interval 144

# current interface ip-address
:do {
    :local ipwm [/ip address get [/ip address find interface=$ifname] address];
    :set ifip [:pick $ipwm 0 [:find $ipwm "/" -1]]; # remove trailing mask
} on-error={:log error "dyndns: can't get ip-address of interface $ifname"};

# resolve ip-address by name
:do {
    :set ddnsip [:resolve $ddnsname];
} on-error={:log error "dyndns: can't resolve ip-address of $ddnsname"};

# update ip-address
:do {
    :if ($ifip != 0.0.0.0 && ($ifip != $ddnsip || $runcount % $interval = 0)) do={
        :log info "dyndns: updating the ip-address at dy.fi" 
        :local str "/nic/update\?hostname=$ddnsname&myip=$ifip" 
        /tool fetch address=dy.fi host=dy.fi src-path=$str mode=http user=$ddnsuser password=$ddnspass dst-path="/dyndns.txt" 
        :local result [/file get dyndns.txt contents]
        :log info ("dyndns: dy.fi update result: " . $result . "end of results")
    }
} on-error={:log error "dyndns: updating ip-address of $ddnsname failed"};
